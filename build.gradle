import com.github.oesolutions.spock.jgit.gradle.Version

plugins {
    id 'com.jfrog.bintray' version '1.8.5'
    id 'java-library'
    id 'maven-publish'
}

group = 'com.github.oesolutions'
version = new Version( '0.0.1', '1.3', '5.9.0.202009080501-r' )

repositories {
    jcenter()
}

dependencies {
    compileOnly( "org.spockframework:spock-core:${version.spockVersion}-groovy-2.5" )
    implementation( "org.eclipse.jgit:org.eclipse.jgit.junit:${version.jgitVersion}" )
}

publishing {
    publications {
        spockExtension( MavenPublication ) {
            from components.java
        }
    }
}

bintray {
    user = project.property( 'bintray_user' )
    key = project.property( 'bintray_api_key' )

    pkg {
        repo = 'maven'
        name = 'spock-jgit'
        licenses = ['Unlicense']
        websiteUrl = 'https://github.com/oesolutions/spock-jgit'
        issueTrackerUrl = 'https://github.com/oesolutions/spock-jgit/issues'
        vcsUrl = 'https://github.com/oesolutions/spock-jgit.git'
        githubRepo = 'oesolutions/spock-jgit'
        githubReleaseNotesFile = 'README.md'
        version {
            name = project.version
            vcsTag = project.version
            mavenCentralSync {
                sync = false
            }
        }
    }

    publications = ['spockExtension']
    // Temporary workaround for https://github.com/bintray/gradle-bintray-plugin/issues/229
    def groupPath = project.group.replaceAll('\\.', '/')
    filesSpec {
        from "${buildDir}/publications/spockExtension"
        include 'module.json'
        into "${groupPath}/${project.name}/${project.version}"
        rename ".*", "${project.name}-${project.version}.module"
    }
}
